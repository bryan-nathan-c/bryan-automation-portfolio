plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Cucumber core & JUnit Platform integration
    testImplementation 'io.cucumber:cucumber-java:7.18.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.18.0'
    // Tambahkan kembali agar runner JUnit4 (@RunWith) dapat dikompilasi
    testImplementation 'io.cucumber:cucumber-junit:7.18.0'

    // Selenium WebDriver
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.15.0'

    // Rest Assured (exclude commons-fileupload transitif)
    testImplementation('io.rest-assured:rest-assured:5.3.2') {
        exclude group: 'commons-fileupload', module: 'commons-fileupload'
    }
    testImplementation('io.rest-assured:json-path:5.3.2') {
        exclude group: 'commons-fileupload', module: 'commons-fileupload'
    }
    testImplementation('io.rest-assured:xml-path:5.3.2') {
        exclude group: 'commons-fileupload', module: 'commons-fileupload'
    }

    // Patched Commons FileUpload untuk fix WS-2019-0379
    testImplementation 'commons-fileupload:commons-fileupload:1.5'

    // WebDriverManager
    testImplementation('io.github.bonigarcia:webdrivermanager:5.5.3') {
        exclude group: 'commons-fileupload', module: 'commons-fileupload'
    }

    // JUnit Jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'

    // SLF4J Simple
    testImplementation 'org.slf4j:slf4j-simple:2.0.9'
}

test {
    useJUnitPlatform()
    systemProperty 'cucumber.publish.enabled', 'false'
    jvmArgs = [
            '-Dcucumber.plugin=html:build/reports/cucumber/html',
            '-Dcucumber.plugin=json:build/reports/cucumber/json/cucumber.json'
    ]
}

// Task runAll dengan Cucumber CLI
tasks.register('runAll', Exec) {
    group = 'verification'
    description = 'Run all Cucumber features via CLI'
    commandLine = [
            'java',
            '-cp', sourceSets.test.runtimeClasspath.asPath,
            'io.cucumber.core.cli.Main',
            '--plugin', 'pretty',
            '--plugin', 'html:build/reports/cucumber/all/html',
            '--plugin', 'json:build/reports/cucumber/all/json/cucumber.json',
            '--glue', 'stepdefinitions',
            'src/test/resources/features'
    ]
}

// Task runWebTests (@web)
tasks.register('runWebTests', Test) {
    useJUnitPlatform()
    systemProperty 'cucumber.filter.tags', '@web'
    jvmArgs = [
            '-Dcucumber.plugin=html:build/reports/cucumber/web/html',
            '-Dcucumber.plugin=json:build/reports/cucumber/web/json/cucumber.json'
    ]
}

// Task runApiTests (@api)
tasks.register('runApiTests', Test) {
    useJUnitPlatform()
    systemProperty 'cucumber.filter.tags', '@api'
    jvmArgs = [
            '-Dcucumber.plugin=html:build/reports/cucumber/api/html',
            '-Dcucumber.plugin=json:build/reports/cucumber/api/json/cucumber.json'
    ]
}
